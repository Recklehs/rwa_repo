plugins {
    id 'java'
    id 'application'
}

group = 'io.rwa'
version = '0.0.1'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

defaultTasks 'clean', 'build'

application {
    mainClass = 'io.rwa.flink.FlinkIndexerApplication'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.flink:flink-java:1.20.0'
    implementation 'org.apache.flink:flink-streaming-java:1.20.0'
    implementation 'org.apache.flink:flink-clients:1.20.0'
    implementation 'org.apache.flink:flink-connector-kafka:3.3.0-1.20'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.2'
    implementation 'org.web3j:core:4.12.3'
    implementation 'org.postgresql:postgresql:42.7.4'
    runtimeOnly 'org.slf4j:slf4j-simple:2.0.13'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.10.2'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.named('jar') {
    archiveFileName = 'rwa-flink-indexer.jar'
    manifest {
        attributes('Main-Class': application.mainClass.get())
    }
}

tasks.register('copyRuntimeDepsForImage', Copy) {
    dependsOn tasks.named('jar')
    from configurations.runtimeClasspath
    into layout.buildDirectory.dir('image-libs')
}
